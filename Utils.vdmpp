class Utils
	functions
		private reduceSeqMeasure[@T, @R]: (@R * @T -> @R) * @R * seq of @T +> nat
		reduceSeqMeasure(f, starter, list) == 
			len list
		;
		
		public reduceSeq[@T, @R]: (@R * @T -> @R) -> @R -> seq of @T -> @R
			reduceSeq(f)(starter)(list) == 
			if(len list = 0)
				then starter
				else reduceSeq[@T, @R](f)(f(starter, hd list))(tl list)
			measure reduceSeqMeasure
		;
		
		private mapFunctionSeqMeasure[@P, @R]: (@P->@R) * seq of @P +> nat
			mapFunctionSeqMeasure(f,list) == 
			len list
		;
		
		public mapFunctionSeq[@P, @R]: (@P->@R) -> seq of @P -> seq of @R
			mapFunctionSeq(f)(list) == 
			if(len list = 0)
				then []
			else [f(hd list)] ^ mapFunctionSeq[@P, @R](f)(tl list)
			
			measure mapFunctionSeqMeasure
		;
		
		private reduceMapAuxMeasure[@L, @R, @S]: (@S * @L * @R -> @S) * @S * map @L to @R * seq of @L +> nat
			reduceMapAuxMeasure(f,starter,m,list) ==
			len list
		;
		
		private reduceMapAux[@L, @R, @S]: (@S * @L * @R -> @S) -> @S -> map @L to @R ->seq of @L -> @S
			reduceMapAux(f)(starter)(m)(list) ==
			if(len list = 0)
				then starter
				else reduceMapAux[@L, @R, @S](f)(f(starter, hd list, m(hd list)))(m)(tl list)
			measure reduceMapAuxMeasure
		;
		
		public reduceMap[@L, @R, @S]: (@S * @L * @R -> @S) -> @S -> map @L to @R -> @S
			reduceMap(f)(starter)(m) ==
			reduceMapAux[@L, @R, @S](f)(starter)(m)(VDMUtil`set2seq[@L](dom m))
		;
		
		private mapFunctionMapAuxMeasure[@L, @R, @S]: (@L*@R->@S) * map @L to @R * seq of @L +> nat
			mapFunctionMapAuxMeasure(f, m, list) ==
			len list
		;
		
		private mapFunctionMapAux[@L, @R, @S]: (@L*@R->@S) -> map @L to @R -> seq of @L -> seq of @S
			mapFunctionMapAux(f)(m)(list) ==
			if(len list = 0)
				then []
			else [f(hd list, m(hd list))] ^ mapFunctionMapAux[@L, @R, @S](f)(m)(tl list)
			
			measure mapFunctionMapAuxMeasure
		;

		public mapFunctionMap[@L, @R, @S]: (@L*@R->@S) -> map @L to @R -> seq of @S
			mapFunctionMap(f)(m) ==
			mapFunctionMapAux[@L, @R, @S](f)(m)(VDMUtil`set2seq[@L](dom m))
		;
		
		public map2set[@L, @R](m: map @L to @R) res: set of (@L*@R) ==
			{ mk_(a,b) | a in set dom m, b in set rng m & b = m(a)}
		;
		
		public map2seq[@L, @R](m: map @L to @R) res: seq of (@L*@R) ==
			VDMUtil`set2seq[(@L*@R)](map2set[@L,@R](m))
		;

end Utils