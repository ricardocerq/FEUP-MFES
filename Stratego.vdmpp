class Stratego
	types
	
		public Point:: 	
			x: nat1
			y: nat1;
												
		public Move:: 	
			src: Point
			dst: Point;
										
		public Character = 	 
			<Flag>
		| <Bomb> 		
		| <Spy> 				
		| <Scout>
		| <Miner>
		| <Sergeant> 
		| <Lieutenant> 
		| <Captain>
		| <Major>
		| <Colonel> 
		| <General> 
		| <Marshal>;
											 
		public Team = 
			<Red>
		| <Blu>
		
								
	values
		
	functions
		
		public getOpponent(pl: Team) res: Team ==
			if pl = <Red> 
				then <Blu> 
				else <Red>
			
			post res <> pl
		;
		
		public validDim(v: nat1) res: bool ==
			v > 0 and v <= StrategoRuleSet`boardSize
		;
		
		public validDimSet() res: set of nat1 ==
			{1, ..., StrategoRuleSet`boardSize}
			
			post forall a in set res & validDim(a)
		;
		
		public validPoint(p: Point) b: bool ==
			validDim(p.x) and validDim(p.y)
		;
		
		public invertedPoint(p: Point) res: Point == 
			mk_Point(p.x, StrategoRuleSet`boardSize - p.y + 1)
				
			pre validPoint(p)
			post validPoint(p) and p.x = res.x and p.y = StrategoRuleSet`boardSize - res.y + 1
		;
		
		public genAllPoints() res: set of Point ==
			 {mk_Point(x,y) | x in set validDimSet() , y in set validDimSet()}
		;
		
		public genPoints(f: Point->bool) res: set of Point ==
			 {p | p in set genAllPoints() & f(p)}
		;
		
		public isInBackRowBlu(p: Point) res: bool ==
			p.y = 1
			
			post res <=> (p.y = 1) --trivial
		;
		
		public isInBackRowRed(p: Point) res: bool ==
			p.y = StrategoRuleSet`boardSize
			
			post res <=> (p.y = StrategoRuleSet`boardSize) --trivial
		;
		
		public isInBackRow(p: Point, pl: Team) res: bool == 
			if pl = <Red>
				then isInBackRowRed(p)
				else isInBackRowBlu(p)
				
			pre validPoint(p)
			post pl = <Red> => ((res = true) <=> isInBackRowRed(p)) and
					 pl = <Blu> => ((res = true) <=> isInBackRowBlu(p))
		;
		
		public isInTeamAreaBlu(p: Point) res: bool ==
			p.y > 0 and p.y <= StrategoRuleSet`boardSize * StrategoRuleSet`sideProportion
			
			post res <=> (p.y > 0 and p.y <= StrategoRuleSet`boardSize * StrategoRuleSet`sideProportion) --trivial
		;
		
		public isInTeamAreaRed(p: Point) res: bool ==
			p.y <= StrategoRuleSet`boardSize and p.y > StrategoRuleSet`boardSize - StrategoRuleSet`boardSize * StrategoRuleSet`sideProportion
			
			post res <=> (p.y <= StrategoRuleSet`boardSize and p.y > StrategoRuleSet`boardSize - StrategoRuleSet`boardSize * StrategoRuleSet`sideProportion) --trivial
		;
		
		public isInTeamArea(p: Point, pl:Team) res: bool ==
			if pl = <Red>
				then isInTeamAreaRed(p)
				else isInTeamAreaBlu(p)
				
			pre validPoint(p)
			post pl = <Red> => ((res = true) <=> isInTeamAreaRed(p)) and
					 pl = <Blu> => ((res = true) <=> isInTeamAreaBlu(p))
		;
 
end Stratego

